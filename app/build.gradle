plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'androidx.navigation.safeargs'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.comp6442.route42"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("boolean", "DEBUG", "true")
        buildConfigField("boolean", "loadData", "true")
        buildConfigField("boolean", "DEMO", "true")
        buildConfigField("String", "testUserEmail", "\"foo@bar.com\"")
        buildConfigField("String", "testUserPassword", "\"password\"")

        buildConfigField("int", "batchSize", "5")
        buildConfigField("int", "intervalLengthInSeconds", "10")
        buildConfigField("int", "demoPostLimit", "500")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.guava:guava:30.1.1-android'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.code.gson:gson:2.8.8'

    // UI
    implementation 'com.google.android.material:material:1.4.0'

    // ----------- Navigation Component ------------
    def nav_version = "2.3.5"
    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    // Jetpack Compose Integration
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha08"

    // ----------- Firebase ------------
    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:28.4.0')

    // Add the dependency for the Firebase SDK for Google Analytics
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore:23.0.3'

    // FirebaseUI for Cloud Firestore
    implementation 'com.firebaseui:firebase-ui-firestore:6.2.1'

    // Declare the dependency for the Cloud Storage library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.firebaseui:firebase-ui-storage:7.2.0'

    // ----------- Glide ------------
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // ----------- Mapbox ------------
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.2.1'
    implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.9.0"
    // Core: Color, text, and networking utility classes.
    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-core:5.8.0'
    // GeoJSON: Java classes that model the GeoJSON spec.
    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-geojson:5.8.0'
    // Turf: Runtime geospatial calculations.
    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:5.8.0'
    implementation "com.mapbox.mapboxsdk:mapbox-android-telemetry:6.1.0"

    // ----------- Tests ---------------
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    testImplementation 'junit:junit:4.+'
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.hamcrest:hamcrest-core:1.1') with module('junit:junit:4.10')
    }
}